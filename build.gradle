apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

configurations {
	pluginjar
}

repositories {
	mavenCentral()

	maven {
		url "https://swt-repo.googlecode.com/svn/repo/"
	}
}

project.ext {
	pluginId = "gui"
	pluginName = "GUI"
	pluginVersion = "0.1.12-alpha"	
	pluginDate = new Date()
	pluginAppMinVersion = "0.1.11-alpha"
	pluginRelease = rootProject.ext.applicationRelease
	pluginConflictsWith = ""
	
	pluginVersionSnapshot = rootProject.ext.applicationVersionSnapshot
	pluginVersionFull = "${pluginVersion}${pluginVersionSnapshot}"	
}

dependencies {
	compile		project(":syncany-lib")
	compile		project(":syncany-util")	
	compile		project(':syncany-cli')

	compile		"commons-net:commons-net:2.2"    
	compile		"commons-beanutils:commons-beanutils:1.9.0"
	compile		"org.eclipse.swt:org.eclipse.swt.${getSwtLibrary()}:4.4"
	compile		"net.sf.corn:corn-cps:1.1.2"
	compile		"org.apache.httpcomponents:httpclient:4.3.4"
	compile		"org.simpleframework:simple-xml:2.7.1"
	compile		"com.google.guava:guava:15.0"
	compile 	"commons-io:commons-io:2.4"	
	compile		"io.undertow:undertow-websockets-jsr:1.0.15.Final"

	pluginjar	"org.eclipse.swt:org.eclipse.swt.${getSwtLibrary()}:4.4"
	pluginjar	"commons-net:commons-net:2.2"    
	pluginjar	"commons-beanutils:commons-beanutils:1.9.0"
	pluginjar	"org.eclipse.swt:org.eclipse.swt.${getSwtLibrary()}:4.4"
	pluginjar	"net.sf.corn:corn-cps:1.1.2"

	testCompile		project(path: ":syncany-lib", configuration: "tests")      
	testCompile		project(path: ":syncany-util", configuration: "tests")
	testCompile		"junit:junit:4.9"
}

// Application plugin specific parameter
mainClassName     = "org.syncany.gui.Launcher"
applicationName   = "syncany"

applicationDistribution.from("${rootProject.projectDir}/") {
	include "AUTHORS.md", "CHANGELOG.md", "LICENSE.md", "README.md"
}

distTar {
	baseName = "syncany"
	version = "$applicationVersionFull"
	compression = Compression.GZIP
	extension = "tar.gz"	
}

distZip {
	baseName = "syncany"
	version = "$applicationVersionFull"
}

// Windows EXE File ////////////////////////////////////////////////////////////
apply plugin: 'launch4j'
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "edu.sc.seis.gradle:launch4j:1.0.6"
    }
}

project.ext {
	clientExeFileName = "syncany.exe"
}

launch4j {
    mainClassName   = "org.syncany.gui.Launcher"
    icon            = "${rootProject.projectDir}/gradle/innosetup/setup-icon.ico"
    launch4jCmd     = "launch4j"
    outputDir       = "launch4j"
    dontWrapJar     = false
    headerType      = "gui"
    outfile         = "${project.clientExeFileName}"
    
    jreMinVersion   = '1.7.0_10'
    
    copyright       = "Syncany"
    
    initialHeapSize = 512
    maxHeapSize     = 1024
    mutexName       = "syncany-gui"
    windowTitle     = "Syncany Client"
}

// Windows Installer ///////////////////////////////////////////////////////////

task installerGui(dependsOn: ["installApp", "launch4j"]) << {
	def innoSetupDir = new File("${buildDir}/innosetup")

	delete innoSetupDir	
	innoSetupDir.mkdir();	
	
	copy {
		from("${rootProject.projectDir}/gradle/innosetup/setup-left.bmp")
		from("${rootProject.projectDir}/gradle/innosetup/setup-top.bmp")
		from("${rootProject.projectDir}/gradle/innosetup/setup-info-before.rtf")		
		from("${rootProject.projectDir}/gradle/innosetup/setup-info-after.rtf")		
		from("${rootProject.projectDir}/gradle/innosetup/setup-icon.ico")		
		into(innoSetupDir)
	}

	copy {
		from("${rootProject.projectDir}/syncany-gui/build/launch4j/${project.clientExeFileName}")
		into("${rootProject.projectDir}/syncany-gui/build/install/syncany")
	}
	
	copy {
		from("${rootProject.projectDir}/gradle/innosetup/setup.gui.iss.skel")
		rename("setup.gui.iss.skel", "setup.iss")
		expand([
			applicationVersion: "${applicationVersion}",
			applicationVersionFull: "${applicationVersionFull}",			
		])
		into(innoSetupDir)
	}

	exec {
		workingDir rootProject.projectDir
		commandLine "iscc ${innoSetupDir}/setup.iss".split()
	}
}

// Task to determine SWT library ///////////////////////////////////////////////

public String getOsName() {
	String osDefinition;
	String os = System.getProperty('os.name').toLowerCase().split()[0]; 
	String arch = System.getProperty("os.arch");
	String realArch = arch.substring(arch.length()-2, arch.length());

	return os + "_" + realArch;
}

public String getSwtLibrary() {
	String osName = getOsName();	
	String swtJarName;
	
	switch(getOsName()) {
		case 'windows_86': swtJarName = 'win32.win32.x86'; break;
		case 'windows_64': swtJarName = 'win32.win32.x86_64'; break; 
		case 'linux_86': swtJarName = 'gtk.linux.x86'; break;
		case 'linux_64': swtJarName = 'gtk.linux.x86_64'; break;
		case 'mac_64': swtJarName = 'cocoa.macosx.x86_64'; break; 
		case 'mac_86': swtJarName = 'cocoa.macosx'; break; 
		default: throw new Exception('Unknown OS');
	}
	
	return swtJarName;
}

// syncany-gui tasks
task(runGui, dependsOn: 'classes', type: JavaExec) {
	main = "${mainClassName}"
	//SWT need -XStartOnFirstThread to run on OSX
	jvmArgs = getOsName().startsWith("mac") ? ['-XstartOnFirstThread'] : []	
	classpath = sourceSets.main.runtimeClasspath
}

apply from: 'core/gradle/gradle/plugins.jar.gradle'
